apply plugin: 'maven-publish'

if (project.plugins.hasPlugin("java-library")) {
    tasks.register('javadocJar', Jar) {
        from javadoc
        archiveClassifier.convention("javadoc")
        archiveClassifier.set("javadoc")
    }
    tasks.register('javaSourcesJar', Jar) {
        from sourceSets.main.java.srcDirs
        archiveClassifier.convention("sources")
        archiveClassifier.set("sources")
    }
} else if (project.plugins.hasPlugin("com.android.library")) {
    tasks.register('androidSourcesJar', Jar) {
        from android.sourceSets.main.java.srcDirs
    }
}

def getPublishVersion(){
    def isWindows = org.gradle.internal.os.OperatingSystem.current().isWindows()
    if (isWindows) {
        return libs.versions.kmvvm.get()
    }else{
        System.getenv("VERSION")
    }
}

afterEvaluate {
    publishing {
        publications {
            github(MavenPublication) {
                if (project.plugins.hasPlugin("com.android.library")) {
                    from components.release
                    artifact androidSourcesJar
                } else if (project.plugins.hasPlugin("java-library")) {
                    from components.java
                    artifact javadocJar
                    artifact javaSourcesJar
                }
                def projectName = project.name
                groupId = 'io.github.catchpig.kmvvm'
                artifactId = projectName
                version = getPublishVersion()

                pom {
                    name = 'kmvvm'
                    description = 'kmvvm'
                    url = 'https://github.com/catchpig/kmvvm'
                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'zhuazhu'
                            name = 'zhuazhu'
                            email = '327785185@qq.com'
                        }
                    }
                    scm {
                        connection = 'https://github.com/catchpig/kmvvm'
                        developerConnection = 'https://github.com/catchpig/kmvvm.git'
                        url = 'https://github.com/catchpig/kmvvm'
                    }
                }
            }
        }
        repositories {
            maven {
                name 'local'
                url "${rootProject.buildDir}"
            }
            maven {
                name = "GitHubPackages"
                url = "https://maven.pkg.github.com/catchpig/kmvvm"
                credentials {
                    username = System.getenv("USERNAME")
                    password = System.getenv("GITHUB_TOKEN")
                }
            }
        }
    }
}
